;; # clojure core.async/flow å…¨é¢ä½“éªŒç¬”è®°
^{:nextjournal.clerk/visibility {:code :hide}
  :nextjournal.clerk/toc true}
(ns core-async-flow
  (:require [clojure.core.async :as a :refer [>! <! >!! <!! go go-loop chan buffer dropping-buffer sliding-buffer promise-chan put! take! close! onto-chan! alt!
                                              pub sub unsub mix admix unmix pipe mult tap untap timeout alts! alts!!]]
            [clojure.core.async.flow :as flow]
            [clojure.core.async.flow-monitor :as fmon]
            [nextjournal.clerk :as clerk]
            [nextjournal.clerk.viewer :as v])
  (:import [java.lang.management ManagementFactory]
           [java.lang Runtime]))


^{:nextjournal.clerk/visibility {:code :hide}}
(clerk/add-viewers!
 [{:pred #(instance? clojure.core.async.impl.channels.ManyToManyChannel %)
   :render-fn '(fn [] [:h1.text-green-500 "ğŸ›£ï¸"])}])


;; ## 0. ä¸ºä»€ä¹ˆæœ‰core.async

^{:nextjournal.clerk/visibility {:code :show}}
(defn get-memory-usage
  "æŸ¥çœ‹å½“å‰jvmçš„å †å†…å­˜, è¿”å›ä¸€ä¸ªmap:
  {:total 536, :free 373, :used 163}"
  []
  (let [runtime (Runtime/getRuntime)]
    {:total (int (/ (.totalMemory runtime) 1e6))
     :free (int (/ (.freeMemory runtime) 1e6))
     :used (int (/ (- (.totalMemory runtime) (.freeMemory runtime)) 1e6))}))

^{:nextjournal.clerk/visibility {:code :show}}
(defn get-thread-count "è·å–æ€»çš„çº¿ç¨‹æ•°é‡" []
  (let [thread-mx-bean (ManagementFactory/getThreadMXBean)]
    (.getThreadCount thread-mx-bean)))


^{:nextjournal.clerk/visibility {:code :show :result :show}}
(clerk/table (let [m (get-memory-usage)]
               [(keys m)
                (vals m)]))


(get-thread-count)

^{:nextjournal.clerk/visibility {:code :show :result :show}}
(clerk/table (let [m (get-memory-usage)]
               [(keys m)
                (vals m)]))


;; **åˆ›å»ºç™¾ä¸‡ä¸ªchannel**
^{:nextjournal.clerk/visibility {:code :show :result :show}}
(dotimes [_ 1e6]
  (go
    (<! (chan))))

^{:nextjournal.clerk/visibility {:code :show :result :show}}
(dotimes [_ 1e3]
  (future
    (Thread/sleep 100000)))



^{:nextjournal.clerk/visibility {:code :show :result :show}}
(clerk/table (let [m (get-memory-usage)]
               [(keys m)
                (vals m)]))


(get-thread-count)

;; ## 1. core.async åŸºç¡€å›é¡¾

;; ### Go å—ä¸ Channel
(def greeting-ch (chan))
(go (>! greeting-ch "ä½ å¥½, core.async")) ; å°†æ¶ˆæ¯æ”¾å…¥ channel
(<!! greeting-ch)                       ; => "ä½ å¥½, core.async"

;; ### <!, >! ä¸ go åä½œ
^{:nextjournal.clerk/visibility {:code :show :result :show}}
(let [requests (chan)
      responses (chan)]
  (go ;; <! ä¼šåœ¨ go ä¸­åœè½¦ç­‰å¾…æ•°æ®, ä¸ä¼šå ç”¨çœŸå®çº¿ç¨‹
    (let [req (<! requests)]
      ;; è¯»å–åç”¨ >! å¼‚æ­¥åœ°å°†å¤„ç†ç»“æœæ”¾å…¥å¦ä¸€ä¸ª channel
      (>! responses (str "å“åº”:" req))))
  ;; >!! ä¼šé˜»å¡å½“å‰çº¿ç¨‹, ç›´åˆ°æœ‰æ¥æ”¶è€…å‡†å¤‡å¥½
  (>!! requests "ping")
  ;; <!! åŒæ ·ä¼šé˜»å¡ç›´åˆ°è¯»åˆ°å€¼
  (<!! responses))

;; ### <!! é˜»å¡ç­‰å¾… go ä¸­äº§å‡ºçš„å€¼
^{:nextjournal.clerk/visibility {:code :show :result :show}}
(let [c (chan)]
  (go
    (>! c :å‡†å¤‡)
    (<! (timeout 20))
    (>! c :å®Œæˆ))
  [(<!! c) (<!! c)])

;; ### >!! é˜»å¡å¼å†™å…¥ç›´åˆ°å€¼è¢«æ¶ˆè´¹
^{:nextjournal.clerk/visibility {:code :show :result :show}}
(let [c (chan)
      ack (promise)]
  (go
    (let [v (<! c)]
      (deliver ack v)))
  {:put-return (>!! c :sync-value)
   :received @ack})

;; ### ç¼“å†²åŒºç¤ºä¾‹

(def drop-ch (chan (dropping-buffer 1)))
(>!! drop-ch :a)
(>!! drop-ch :b)
(<!! drop-ch) ;; æ”¾ä¸è¿›å»çš„:bä¼šè¢«dropæ‰

(def slide-ch (chan (sliding-buffer 1)))
(>!! slide-ch :a)
(>!! slide-ch :b)
(<!! slide-ch)

;; ### alts! ä»å¤šä¸ª channel è¯»å–
(let [c1 (chan)
      c2 (chan)]
  (go (<! (timeout 100)) (>! c1 :c1))
  (go (<! (timeout 50)) (>! c2 :c2))
  (<!! (go (alts! [c1 c2]))))

;; ### ä½¿ç”¨ Transducer ç»„åˆ channel çš„æ•°æ®å¤„ç†
^{:nextjournal.clerk/visibility {:code :show :result :show}}
(let [xf (comp (map inc)
               (filter even?)
               (map #(* % %)))
      transducer-ch (chan 10 xf)]
  ;; onto-chan! ä¼šè‡ªåŠ¨å…³é—­ channel, <!! é˜»å¡ç›´åˆ°ä¼ è¾“å®Œæ¯•
  (<!! (a/onto-chan! transducer-ch (range 6)))
  (<!! (a/into [] transducer-ch)))

;; ### pipeline ä¸ transducer çš„é…åˆ
^{:nextjournal.clerk/visibility {:code :show :result :show}}
(let [xf (comp (map #(* % %))
               (filter odd?))
      in (chan 5)
      out (chan 5)]
  (a/pipeline 4 out xf in)
  (<!! (a/onto-chan! in (range 8)))
  (<!! (a/into [] out)))

;; ### å¼‚å¸¸å¤„ç†
^{:nextjournal.clerk/visibility {:code :show :result :show}}
(let [in (chan)
      out (chan)
      errors (chan)]
  (go-loop []
    (if-some [value (<! in)]
      (do
        (try
          (put! out (/ 10 value))
          (catch Throwable t
            (put! errors {:input value
                          :message (.getMessage t)})))
        (recur))
      (do
        (close! out)
        (close! errors))))


  (>!! in 2)
  (>!! in 0)
  (>!! in 5)
  (close! in)



  {:ok-values (loop [acc []]
                (if-some [v (<!! out)]
                  (recur (conj acc v))
                  acc))
   :errors (loop [acc []]
             (if-some [err (<!! errors)]
               (recur (conj acc err))
               acc))})

;; ## 2. è¿›é˜¶ Channel æ“ä½œ

;; ### ç®¡é“(pipe)
(let [in (chan)
      out (chan)]
  (pipe in out)
  (>!! in :hello)
  (<!! out))

;; ### å¤šè·¯åˆ†å‘(mult/tap)
(let [source (chan)
      m (mult source)
      c1 (chan)
      c2 (chan)]
  (tap m c1)
  (tap m c2)
  (>!! source :hi)
  [(<!! c1) (<!! c2)])

;; ### è®¢é˜…(pub/sub)
(let [source (chan)
      p (pub source :topic)
      t1 (chan)
      t2 (chan)]
  (sub p :foo t1) ;; ä»pä¸­è®¢é˜… :foo ä¸»é¢˜
  (sub p :bar t2) ;; ä»pä¸­è®¢é˜… :bar ä¸»é¢˜
  (>!! source {:topic :foo :msg 1})
  (>!! source {:topic :bar :msg 2})
  [(<!! t1) (<!! t2)])

;; ### mix / admix
(let [a (chan)
      b (chan)
      out (chan)
      m (mix out)]
  (admix m a)
  (admix m b)
  (go (>!! a :a))
  (go (>!! b :b))
  [(<!! out) (<!! out)])

;; ### alt!
(let [chan-a (chan)
      chan-b (chan)
      chan-c (chan)
      chan-d (chan)
      chan-e (chan)
      chan-f (chan)
      chan-g (chan)
      chan-h (chan)
      chan-i (chan)]
  (go-loop [i 10]
    (when (> i 0)
      (println
       (alt!
         chan-a :receive-from-a               ;; å•ä¸ªæ¥æ”¶chan, è¿”å›è¡¨è¾¾å¼
         chan-b ([v] (println "b:" v))        ;; å•ä¸ªæ¥æ”¶chan, ä¸€ä¸ªå‚æ•°çš„callback func
         [chan-c chan-d] :receive-from-c-or-d ;; å¤šä¸ªæ¥æ”¶chan, è¿”å›è¡¨è¾¾å¼
         [chan-e chan-f] ([v c]               ;; å¤šä¸ªæ¥æ”¶chan, ä¸¤ä¸ªå‚æ•°çš„call-back
                          (println "receive:" v "from" c))
         [[chan-g "v"]]  :send-to-g           ;; å•ä¸ªå‘é€chan,
         [[chan-h "h"]]  ([v]                 ;; å•ä¸ªå‘é€chan, ä¸€ä¸ªå‚æ•°çš„call-back
                          (println "from h:" v))
         [[chan-i "i"]]  ([v c]               ;; å•ä¸ªå‘é€chan, ä¸¤ä¸ªå‚æ•°çš„call-back
                          (println "send:" v "from" c))
         ;;(timeout 100) :timeout
         :default 42
         )))
    (recur (dec i)))

  (put! chan-a "msg...") ;; return=> :receive-from-a
  (put! chan-b "msg...")   ;; print => b: msg...
  (put! chan-c "msg...")   ;; return=> :receive-from-c-or-d
  (put! chan-d "msg...")   ;; return=> :receive-from-c-or-d
  (put! chan-e "msg...")  ;; print=> receive: msg... from #object...
  (put! chan-f "msg...")  ;; print=> receive: msg... from #object...
  [(<!! chan-g)           ;; print=> :send-to-g
   (<!! chan-h)           ;; print=> from h: true
   (<!! chan-i)]           ;; print=> from h: true
  )

;; ### å…³é—­channel

;; 1. close! æ˜¯ä¸ªå‰¯ä½œç”¨å‡½æ•°, å§‹ç»ˆè¿”å›nil
;; 1. ä»å…³é—­çš„channelä¸­, å–å‡ºçš„å§‹ç»ˆæ˜¯nil
;; 2. æ”¾å…¥å…³é—­çš„channel, è¿”å›å€¼å§‹ç»ˆæ˜¯false
(let [c (chan)]
  [(close! c)
   (<!! c)
   (put! c 100)])


;; **æ³¨æ„**: ä»¥å‰æ”¾å…¥çš„å€¼, è¿˜å¯ä»¥æ‹¿åˆ°, æ‹¿åˆ°ä¹‹åæ°¸è¿œæ˜¯nil
(let [c (chan)]
  (put! c 100)
  [(close! c)
   (<!! c)   ;; æ‹¿åˆ°100
   (<!! c)   ;; æ‹¿åˆ°nil
   (put! c 100)])

;; å¯¹pipeçš„å½±å“
(let [in (chan)
      out (chan)]
  (pipe in out)
  (>!! in :hello)
  (close! in)
  [(<!! out)
   (<!! in)    ;; ä¸Šæ¸¸in è¢«æˆ‘ä»¬å…³é—­
   (<!! out)]  ;; ä¸‹æ¸¸out ä¼šè¿å¸¦å…³é—­
  )

;; å¯¹å¤šè·¯åˆ†å‘çš„å½±å“
(let [source (chan)
      m (mult source)
      c1 (chan)
      c2 (chan)]
  (tap m c1)
  (tap m c2)
  (>!! source :hi)
  [(<!! c1) (<!! c2)
   (close! source)  ;; å…³é—­source
   (<!! c1) (<!! c2)]) ;; æ‰€æœ‰çš„tapéƒ½ä¼šå…³é—­

;; å¯¹pub/subçš„å½±å“
(let [source (chan)
      p (pub source :topic)
      t1 (chan)
      t2 (chan)]
  (sub p :foo t1) ;; ä»pä¸­è®¢é˜… :foo ä¸»é¢˜
  (sub p :bar t2) ;; ä»pä¸­è®¢é˜… :bar ä¸»é¢˜
  (>!! source {:topic :foo :msg 1})
  (>!! source {:topic :bar :msg 2})
  [(<!! t1) (<!! t2)
   (close! source)       ;; å…³é—­source
   (<!! t1) (<!! t2)])   ;; æ‰€æœ‰çš„è®¢é˜…éƒ½ä¼šå…³é—­

;; ### go çº¿ç¨‹æ± ï¼šåœè½¦ä¸é˜»å¡çš„å·®åˆ«
^{:nextjournal.clerk/visibility {:code :show :result :show}}
(let [result (chan)
      go-count 64]
  (dotimes [_ go-count]
    (go
      ;; ä½¿ç”¨ <! ä¼šåœ¨ç­‰å¾… timeout æ—¶åœè½¦ï¼Œè®©å‡ºçº¿ç¨‹
      (<! (timeout 10))))
  (go (>! result :done))
  (let [[value port] (alts!! [result (timeout 50)])]
    {:value value
     :timed-out? (not= port result)
     :parked-go-count go-count}))

;; ### é”™è¯¯ç¤ºä¾‹ï¼š<!! é˜»å¡å¯¼è‡´çº¿ç¨‹æ± è€—å°½
^{:nextjournal.clerk/visibility {:code :show :result :show}}
(let [result (chan)
      go-count 64]
  (dotimes [_ go-count]
    (go
      ;; é”™è¯¯ï¼š<!! åœ¨ go ä¸­ä¼šé˜»å¡çœŸå®çº¿ç¨‹
      (<!! (timeout 200))))
  (go (>! result :done))
  (let [[value port] (alts!! [result (timeout 50)])]
    {:value value
     :timed-out? (not= port result)
     :blocked-go-count go-count}))

;; ### go çº¿ç¨‹æ± å¹¶å‘åº¦ä¸Šé™æ¼”ç¤º
^{:nextjournal.clerk/visibility {:code :show :result :show}}
(let [task-count 20
      ;; done é€šé“è®¾ç½®æˆè¶³å¤Ÿçš„ç¼“å†²ï¼Œé¿å… >! å†æ¬¡åœè½¦å½±å“æµ‹é‡
      done (chan task-count)
      start (System/nanoTime)]
  (dotimes [i task-count]
    (go
      ;; Thread/sleep ä¼šå ç”¨çœŸå®çº¿ç¨‹ï¼Œæœ€å¤šåªæœ‰å›ºå®šæ•°é‡çš„ go çº¿ç¨‹å¯ç”¨
      (Thread/sleep 200)
      (>! done {:task i
                :finished-ms (/ (- (System/nanoTime) start) 1e6)})))
  (let [results (repeatedly task-count #(<!! done))
        batches (->> results
                     (group-by #(int (Math/floor (/ (:finished-ms %) 200))))
                     (map (fn [[slot xs]]
                            {:slot slot
                             :completed (count xs)
                             :finished-range [(apply min (map :finished-ms xs))
                                              (apply max (map :finished-ms xs))]}))
                     (sort-by :slot))]
    {:task-count task-count
     :sleep-ms 200
     :batch-summary batches}))

;; ### ä¿®æ­£æ–¹å¼ï¼šå°†é˜»å¡æ“ä½œæ”¾å…¥ thread æˆ– pipeline-blocking
^{:nextjournal.clerk/visibility {:code :show :result :show}}
(let [result (chan)
      worker-count 64]
  (dotimes [_ worker-count]
    (a/thread
      ;; thread ä½¿ç”¨çœŸå®çº¿ç¨‹, å…è®¸é˜»å¡è°ƒç”¨
      (<!! (timeout 200))))
  (go (>! result :done))
  (let [[value port] (alts!! [result (timeout 50)])]
    {:value value
     :timed-out? (not= port result)
     :worker-count worker-count}))

;; ## 3. flow ç®€å•ç¤ºä¾‹

;; ### ä½¿ç”¨ flow è¿æ¥å¤„ç†æ­¥éª¤

(defn stat-gen
  "ç”Ÿæˆä¸€ä¸ªä»‹äº min (åŒ…å«) å’Œ max (ä¸åŒ…å«) ä¹‹é—´çš„éšæœºå€¼, å¹¶å°†å…¶å†™å…¥ out é€šé“.
   åœ¨ stop-atom è¢«æ ‡è®°ä¸º true ä¹‹å‰, æ¯æ¬¡å†™å…¥ä¹‹é—´ä¼šç­‰å¾… wait æ¯«ç§’. "
  ([out min max wait stop-atom]
   (loop []
     (let [val (+ min (rand-int (- max min)))
           put (a/>!! out val)]
       (println "stat-gen" (System/identityHashCode stop-atom) val put (not @stop-atom))
       (when (and put (not @stop-atom))
         (^[long] Thread/sleep wait)
         (recur))))))

(defn source
  "éšæœºæ•°ç”Ÿæˆ"
  ;; æè¿°
  ([] {:params {:min "ç”Ÿæˆä¸‹é™"
                :max "ç”Ÿæˆä¸Šé™"
                :wait "æ¯«ç§’é—´éš”"}
       :outs {:out "éšæœºæ•°çš„è¾“å‡ºchannel"}})

  ;; åˆå§‹åŒ–çŠ¶æ€, çŠ¶æ€mapæ˜¯çŠ¶æ€åˆ‡æ¢çš„å‚æ•°
  ([args]
   (assoc args
          ::flow/in-ports {:stat (a/chan 100)}
          :stop (atom false)))

  ;; çŠ¶æ€åˆ‡æ¢
  ([{:keys [min max wait ::flow/in-ports] :as state} transition]
   (println "transition" transition)
   (case transition
     ::flow/resume
     (let [stop-atom (atom false)]
       (future (stat-gen (:stat in-ports) min max wait stop-atom))
       (assoc state :stop stop-atom))

     (::flow/pause ::flow/stop)
     (do
       (reset! (:stop state) true)
       state)))

  ;; æ•°æ®å¤„ç†
  ([state in msg]
   (println "source transform" in msg)
   [state (when (= in :stat) {:out [msg]})]))

(defn aggregator
  ;; æè¿°
  ([] {:params {:min "å‘Šè­¦é˜ˆå€¼ä¸‹é™, ä½äºåˆ™å‘Šè­¦"
                :max "å‘Šè­¦é˜ˆå€¼ä¸Šé™é™, é«˜äºåˆ™å‘Šè­¦"}
       :ins {:stat "æ”¶å–æ•°æ®çš„channel"
             :poke "è§¦å‘ç³»ç»Ÿæ—¥å¿—çš„channel"}
       :outs {:alert "å‘Šè­¦channel {:val value, :error :high|:low"}
       :workload :compute
       })

  ;; åˆå§‹åŒ–
  ([args] (assoc args :vals []))

  ;; çŠ¶æ€è½¬åŒ–
  ([state transition] state)

  ;; æ•°æ®å¤„ç†
  ([{:keys [min max vals] :as state} input-id msg]
   (case input-id
     :stat (let [state' (assoc state :vals (conj vals msg))
                 msgs (cond
                        (< msg min) {:alert [{:val msg, :error :low}]}
                        (< max msg) {:alert [{:val msg, :error :high}]}
                        :else nil)]
             [state' msgs])
     :poke [(assoc state :vals [])
            {::flow/report (if (empty? vals)
                             [{:count 0}]
                             [{:avg (/ (double (reduce + vals)) (count vals))
                               :count (count vals)}])}]
     [state nil])))

(comment
  ;; æµ‹è¯• aggregator å‘Šè­¦
  (let [state {:min 1 :max 5 :vals []}
        [state' msgs'] (aggregator state :stat 100)]
    (assert (= msgs' {:alert [{:val 100, :error :high}]})))
  )


(defn scheduler
  ;; æè¿°
  ([] {:params {:wait "æ”¶é›†ç³»ç»Ÿæ•°æ®çš„æ—¶é—´é—´éš”"}
       :outs {:out "æ”¶é›†è§¦å‘channel"}})

  ;; åˆå§‹åŒ–
  ([args]
   (assoc args
          ::flow/in-ports {:alarm (a/chan 10)}
          :stop (atom false)))

  ;; çŠ¶æ€è½¬åŒ–
  ([{:keys [wait ::flow/in-ports] :as state} transition]
   (println "scheduler transition" transition state transition)
   (case transition
     ::flow/resume
     (let [stop-atom (atom false)]
       (future (loop []
                 (let [put (a/>!! (:alarm in-ports) true)]
                   (when (and put (not @stop-atom))
                     (^[long] Thread/sleep wait)
                     (recur)))))
       (assoc state :stop stop-atom))

     (::flow/pause ::flow/stop)
     (do
       (reset! (:stop state) true)
       state)))

  ;; æ•°æ®å¤„ç†
  ([state in msg]
   [state (when (= in :alarm) {:out [true]})]))

(defn printer
  ;; æè¿°
  ([] {:params {:prefix "Log message prefix"}
       :ins {:in "Channel to receive messages"}})

  ;; åˆå§‹åŒ–
  ([state] state)

  ;; çŠ¶æ€
  ([state _transition] state)

  ;; æ•°æ®å¤„ç†
  ([{:keys [prefix] :as state} _in msg]
   (println prefix msg)
   [state nil]))

(defn create-flow
  []
  (flow/create-flow
   {:procs {:generator {:args {:min 0 :max 12 :wait 500} :proc (flow/process #'source)}
            :aggregator {:args {:min 1 :max 10} :proc (flow/process #'aggregator)}
            :scheduler {:args {:wait 3000} :proc (flow/process #'scheduler)}
            :notifier {:args {:prefix "Alert: "} :proc (flow/process #'printer)
                       :chan-opts {:in {:buf-or-n (a/sliding-buffer 3)}}}}
    :conns [[[:generator :out] [:aggregator :stat]]
            [[:scheduler :out] [:aggregator :poke]]
            [[:aggregator :alert] [:notifier :in]]]}))

(comment
  (def f (create-flow))
  (def chs (flow/start f))
  (flow/resume f)
  (flow/pause f)
  (flow/stop f)

  (def server (fmon/start-server {:flow f}))
  (fmon/stop-server server)

  @(flow/inject f [:aggregator :poke] [true])
  @(flow/inject f [:aggregator :stat] ["abc1000"]) ;; trigger an alert
  @(flow/inject f [:notifier :in] [:sandwich])

  (def report-chan (:report-chan chs))
  (flow/ping f)
  (a/poll! report-chan)
  (def error-chan (:error-chan chs))
  (a/poll! error-chan)

  (flow/stop f)
  (a/close! stat-chan)

  @(flow/inject f [:aggregator :poke] [true])

  (require '[clojure.datafy :as datafy])
  (datafy/datafy f)

  (require '[clojure.core.async.flow-static :refer [graph]])
  (graph f)

  )

;; ## 4. Flow Monitor å¯è§†åŒ–

(def monitor-port 9998)

(defonce flow-monitor-instance (atom nil))

(defn start-flow-monitor! []
  ;; å¯åŠ¨ flow + monitor, å¹¶ç¼“å­˜å®ä¾‹, ä¿è¯åœ¨ Notebook ä¸­å¤ç”¨
  (or @flow-monitor-instance
      (let [flow (create-flow)
            _chs (flow/start flow)
            _ (flow/resume flow)
            server (fmon/start-server {:flow flow
                                       :port monitor-port})
            instance {:flow flow
                      :server server
                      :port monitor-port}]
        (reset! flow-monitor-instance instance)
        instance)))

(defn stop-flow-monitor! []
  ;; Notebook ç»“æŸåå¯è°ƒç”¨ (stop-flow-monitor!) é‡Šæ”¾èµ„æº
  (when-let [{:keys [flow server]} @flow-monitor-instance]
    (flow/stop flow)
    (fmon/stop-server server)
    (reset! flow-monitor-instance nil)))

(defn flow-monitor-iframe []
  (let [{:keys [port]} (start-flow-monitor!)]
    [:iframe {:width 1100
              :height 720
              :style {:border "1px solid #444"
                      :border-radius "6px"}
              :src (str "http://localhost:" port "/")}]))

(clerk/html
 (flow-monitor-iframe))

;; å¦‚æœéœ€è¦æ‰‹åŠ¨åœæ­¢ç›‘æ§æœåŠ¡, åœ¨ REPL ä¸­æ‰§è¡Œ (stop-flow-monitor!)
(comment

  ;; start Clerk's built-in webserver on the default port 7777, opening the browser when done
  (clerk/serve! {:browse true})

  ;; either call `clerk/show!` explicitly
  (clerk/show! "notebooks/core_async_flow.clj")

  ;; or let Clerk watch the given `:paths` for changes
  (clerk/serve! {:watch-paths ["notebooks" "src"]})

  ;; start with watcher and show filter function to enable notebook pinning
  (clerk/serve! {:watch-paths ["notebooks" "src"] :show-filter-fn #(clojure.string/starts-with? % "notebooks")})

  ;; Build a html file from the given notebook notebooks.
  ;; See the docstring for more options.
  (clerk/build! {:paths ["notebooks/core_async_flow.clj"]})

  )
